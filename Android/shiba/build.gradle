apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'


android {
    compileSdkVersion 28

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.antlr:antlr4:4.7.1'
    // use ANTLR version 4
    implementation 'com.android.support:appcompat-v7:28.+'
    implementation 'org.mozilla:rhino:1.7.10'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:gridlayout-v7:28.+'
    implementation 'com.android.support:recyclerview-v7:28.+'
    implementation 'com.android.support.constraint:constraint-layout:1.+'
}




task makeJar(type: Copy) {
    delete 'build/libs/shiba.jar'
    from('build/intermediates/bundles/release/')
    into('build/libs/')
    include('classes.jar')
    rename('classes.jar', 'shiba.jar')
}

makeJar.dependsOn(build)

def siteUrl = 'https://github.com/Tlaster/Shiba'
def gitUrl = 'https://github.com/Tlaster/Shiba.git'
def issueUrl = 'https://github.com/Tlaster/Shiba/issues'
def buildNum = System.getenv('TRAVIS_BUILD_NUMBER')

group = "shiba"
version = "0.0.$buildNum"

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'Lightweight Cross-Platform UI framework'
                url siteUrl
                licenses {
                    license {
                        name 'Lightweight Cross-Platform UI framework'
                        url 'https://github.com/Tlaster/Shiba/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id 'tlaster'
                        name 'tlaster'
                        email 'tlaster@outlook.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = "Shiba"
        name = "Shiba"
        desc = "Lightweight Cross-Platform UI framework"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = issueUrl
        licenses = ["MIT"]
        labels = ['android']
        publish = true
        publicDownloadNumbers = true
    }
}