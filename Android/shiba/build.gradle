apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'


android {
    compileSdkVersion 28

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.+"

    implementation 'androidx.appcompat:appcompat:1.+'
    implementation 'androidx.gridlayout:gridlayout:1.+'
    implementation 'androidx.recyclerview:recyclerview:1.+'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'

    implementation 'com.github.LiquidPlayer:LiquidCore:0.6.+'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.+'

    implementation 'com.github.kittinunf.fuel:fuel:1.+'
    implementation 'com.github.kittinunf.fuel:fuel-android:1.+'
    implementation 'com.github.kittinunf.fuel:fuel-coroutines:1.+'
    implementation 'com.github.kittinunf.fuel:fuel-jackson:1.+'


    testImplementation 'junit:junit:4.12'
    androidTestImplementation "androidx.test.ext:junit:1.1.0"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
}

def siteUrl = 'https://github.com/Tlaster/Shiba'
def gitUrl = 'https://github.com/Tlaster/Shiba.git'
def issueUrl = 'https://github.com/Tlaster/Shiba/issues'
def buildNum = System.getenv('TRAVIS_BUILD_NUMBER')

group = "moe.tlaster"
version = "0.0.$buildNum"

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'Lightweight Cross-Platform UI framework'
                url siteUrl
                licenses {
                    license {
                        name 'Lightweight Cross-Platform UI framework'
                        url 'https://github.com/Tlaster/Shiba/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id 'tlaster'
                        name 'tlaster'
                        email 'tlaster@outlook.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = "Shiba"
        name = "Shiba"
        desc = "Lightweight Cross-Platform UI framework"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = issueUrl
        licenses = ["MIT"]
        labels = ['android']
        publish = true
        publicDownloadNumbers = true
    }
}