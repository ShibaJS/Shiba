apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def siteUrl = "https://github.com/Tlaster/Shiba"
def gitUrl = siteUrl + ".git"
def issueUrl = siteUrl + "/issues"
def buildNum = System.getenv("TRAVIS_BUILD_NUMBER") ?: 0
group = "moe.tlaster"
version = "0.3.$buildNum"

project.ext {
    mavDevelopers = ["tlaster":"tlaster"]
    mavSiteUrl = siteUrl
    mavGitUrl = gitUrl
    mavProjectName = 'Shiba'
    mavPublishToInternalRepo = true
    mavRepoInternalUrl = "build/"
    mavLibraryLicenses = ["MIT":'https://opensource.org/licenses/mit-license.php']
    mavLibraryDescription = "Lightweight Cross-Platform UI framework"
    mavVersion = version
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/libs/shiba-$version-sources.jar") {
                classifier "sources"
            }
            artifact("$buildDir/libs/shiba-$version-javadoc.jar") {
                classifier "javadoc"
            }
            groupId this.group
            artifactId 'shiba'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    publications = ['Production']
    pkg {
        repo = 'Shiba'
        name = 'Shiba'
        description = "Lightweight Cross-Platform UI framework"
        publish = true
        publicDownloadNumbers = true
        licenses = ['MIT']
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = issueUrl
    }
}
apply from: 'https://raw.githubusercontent.com/sky-uk/gradle-maven-plugin/master/gradle-mavenizer.gradle'
